FUNCTION_BLOCK gorjeta	// Block definition (there may be more than one block per file)

// Define input variables
VAR_INPUT	
	servico : REAL;
	refeicao : REAL;
END_VAR

// Define output variable
VAR_OUTPUT	
	gorjeta : REAL;
END_VAR

// Fuzzify input variable 'servico': {'ruim', 'bom' , 'excelente'}
FUZZIFY servico 	
	TERM ruim := (0, 1) (4, 0); 
	TERM bom := (1, 0) (4,1) (6,1) (9,0);
	TERM excelente := (6, 0) (9, 1);
END_FUZZIFY

// Fuzzify input variable 'refeicao': { 'rancosa', 'deliciosa' }
FUZZIFY refeicao			
	TERM rancosa := (0, 1) (1, 1) (3,0) ;
	TERM deliciosa := (7,0) (9,1);
END_FUZZIFY

// Defuzzzify output variable 'gorjeta' : {'baixa', 'media', 'alta' }
DEFUZZIFY gorjeta			
	TERM baixa := (0,0) (5,1) (10,0);
	TERM media := (10,0) (15,1) (20,0);
	TERM alta := (20,0) (25,1) (30,0);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgans Law)
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

	RULE 1 : IF servico IS ruim OR refeicao IS rancosa THEN gorjeta IS baixa;
	RULE 2 : IF servico IS bom THEN gorjeta IS media; 
	RULE 3 : IF servico IS excelente AND refeicao IS deliciosa THEN gorjeta IS alta;
END_RULEBLOCK

END_FUNCTION_BLOCK